#nop TorilMUD Helper Bot, for use with gobot executable

#CONFIG		{AUTO TAB}		{5000}
#CONFIG		{BUFFER SIZE}	{100000}
#CONFIG		{CHARSET}		{ASCII}
#CONFIG		{CONNECT RETRY}	{360}
#CONFIG		{COMMAND COLOR}	{<078>}
#CONFIG		{COMMAND ECHO}	{ON}
#CONFIG		{HISTORY SIZE}	{2500}
#CONFIG		{LOG}			{RAW}
#nop Without packet patch, some text disappears.
#nop With a high value, the prompt gets overwritten.
#CONFIG		{PACKET PATCH}	{0.05}
#CONFIG		{REPEAT ENTER}	{ON}
#CONFIG		{REPEAT CHAR}	{!}
#CONFIG		{SCROLL LOCK}	{ON}
#CONFIG		{SPEEDWALK}		{ON}
#CONFIG		{TINTIN CHAR}	{#}
#CONFIG		{VERBATIM}		{OFF}
#CONFIG		{VERBATIM CHAR}	{\}
#CONFIG		{VERBOSE}		{OFF}
#CONFIG		{WORDWRAP}		{OFF}
#CONFIG		{256 COLORS}	{ON}
#PATHDIR	{n}		{s}		{1}
#PATHDIR	{e}		{w}		{2}
#PATHDIR	{s}		{n}		{4}
#PATHDIR	{w}		{e}		{8}
#PATHDIR	{u}		{d}		{16}
#PATHDIR	{d}		{u}		{32}


#nop tokens.tin sets the following variables to correct info
#var {acct} {Account}
#var {char} {Character}
#var {cpw} {Password}
#class tokens read tokens.tin


#var {dot} {.}
#alias {logon} {
	#format {logdate} {%t} {%Y.%m.%d};
	#log append logs/$char$dot$logdate${dot}log;
}
#alias {login} {
	#act {^Account name:} {
		#delay (1) {logon;$acct;$cpw;$char;time;};
		#unact {^Account name:};
	};
	#session $char torilmud.com 9999;
	#split {0} {0};
}
#tick {Timer} {time;who} {30}
#act {^Time elapsed since boot-up: %1} {#system gobot -time %1 &}

#event {PROGRAM START} {login}
#event {SESSION DISCONNECTED} {login}
#event {SESSION TIMED OUT} {login}
#event {DAY} {#system gobot -bak}
#act {That account does not exist. Please enter an account or} {#delay 3 #zap}
#act {That character doesn't exist! Please try again.} {#delay 3 #zap}


#act {^%w tells you '%2'$} {
	#format {time} {%t} {%F %T};
	#line log {logs/tells.log} {$time - %0};

	#var {chat} {%2};
	#nop Prevent script hacking;
	#replace {chat} {"} {'};
	#replace {chat} {\} {};
	#nop Remove account labeling;
	#replace {chat} {@} {};
	#script {answer} {gobot -char "%1" -tell "$chat"};
	#foreach {$answer} {cmd} {
		#if {"$cmd" != "%d"} {$cmd};
	};
}
#act {You tell %w '%2'$} {
	#format {time} {%t} {%F %T};
	#line log {logs/tells.log} {$time - %0};
}
#act {No-one by that name here...$} {
	#format {time} {%t} {%F %T};
	#line log {logs/tells.log} {$time - %0};
}


#nop Batch Who Check
#alias {parsewho} {#script {who} {gobot -who "$ppl"}}
#act {^-=-=-=-=-=-=-=-=-=-=-=-=-$} {
	#act {^$} {
		#unact {%%%0};
		#unact {^$};
		#replace {ppl} { (AFK)} {};
		#replace {ppl} { (RP)} {};
		#replace {ppl} {(inv) } {};
		#replace {ppl} {(In Dark)} {};
		#replace {ppl} {(Daylight)} {};
		parsewho;
		#foreach {$who} {cmd} {
			#if {"$cmd" != "%d"} {$cmd};
		};
		#var {who} {};
	}{2};
	#var {ppl} {};
	#var {who} {};
	#act {%%0} {
		#var {ppl} {$ppl|%%0};
	}{3};
}{5}

#nop Individual Checks
#act {^[ %d %+] %w %*(%*) (@%*)} {
	#var {race} {%5};
	#replace {race} {)%*} {};
	#script {answer} {gobot -char %3 -class "%2" -lvl %1 -race "$race" -acct %6};
	#foreach {$answer} {cmd} {
		#if {"$cmd" != "%d"} {$cmd};
	};
}
#act {^[%.%. %+] %w %*(%*) (@%*)} {
	#var {race} {%6};
	#replace {race} {)%*} {};
	#script {answer} {gobot -char %4 -class "%3" -lvl %1"%2" -race "$race" -acct %7};
	#foreach {$answer} {cmd} {
		#if {"$cmd" != "%d"} {$cmd};
	};
}

